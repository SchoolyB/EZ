/*
 * control_flow.ez - Conditionals and loops
 *
 * EZ uses:
 *   - if / or / otherwise (not else if / else)
 *   - when / is / default (pattern matching)
 *   - for with range()
 *   - for_each for collections
 *   - as_long_as (like while)
 *   - loop (infinite loop)
 */

import @std
using std

const Season enum {
    SPRING,
    SUMMER,
    FALL,
    WINTER
}

do main() {
    println("=== Control Flow in EZ ===")
    println("")

    // if / or / otherwise
    println("-- Conditionals --")
    temp score int = 85

    if score >= 90 {
        println("Grade: A")
    } or score >= 80 {
        println("Grade: B")
    } or score >= 70 {
        println("Grade: C")
    } otherwise {
        println("Grade: F")
    }

    // when/is with default (pattern matching)
    println("")
    println("-- When/Is with default --")
    temp day int = 6
    when day {
        is 1, 2, 3, 4, 5 { println("Weekday") }
        is 6, 7 { println("Weekend!") }
        default { println("Invalid day") }
    }

    // when/is with range matching
    println("")
    println("-- When/Is with range --")
    when score {
        is range(90, 101) { println("Letter grade: A") }
        is range(80, 90) { println("Letter grade: B") }
        is range(70, 80) { println("Letter grade: C") }
        is range(60, 70) { println("Letter grade: D") }
        default { println("Letter grade: F") }
    }

    // #strict when/is (must cover all enum values, no default)
    println("")
    println("-- Strict When/Is (enum) --")
    temp current = Season.SUMMER

    #strict
    when current {
        is Season.SPRING { println("Flowers blooming") }
        is Season.SUMMER { println("Sun is shining") }
        is Season.FALL { println("Leaves falling") }
        is Season.WINTER { println("Snow is falling") }
    }

    // for loop with range
    println("")
    println("-- For loop with range --")
    println("Counting 1 to 5:")
    for i in range(1, 6) {
        println("  ${i}")
    }

    // Note: Descending ranges like range(5, 0) are now check-time errors (E9005)
    // Use as_long_as with decrement if you need to count backwards:
    println("")
    println("-- Counting backwards --")
    println("Countdown from 5:")
    temp n int = 5
    as_long_as n > 0 {
        println("  ${n}")
        n--
    }

    // for_each loop
    println("")
    println("-- For_each loop --")
    temp fruits [string] = {"apple", "banana", "cherry"}
    println("Fruits:")
    for_each fruit in fruits {
        println("  - ${fruit}")
    }

    // for_each with index
    println("")
    println("-- For_each with index --")
    println("Numbered fruits:")
    for_each i, fruit in fruits {
        println("  ${i}: ${fruit}")
    }

    // for_each with string
    println("")
    println("Letters in 'EZ':")
    for_each ch in "EZ" {
        println("  ${ch}")
    }

    // as_long_as loop (while equivalent)
    println("")
    println("-- As_long_as loop --")
    temp count int = 0
    as_long_as count < 3 {
        println("  count = ${count}")
        count++
    }

    // loop (infinite loop, use break to exit)
    println("")
    println("-- Loop (infinite loop) --")
    temp x int = 0
    loop {
        if x == 3 {
            break
        }
        println("  x = ${x}")
        x++
    }

    // break and continue
    println("")
    println("-- Break and Continue --")
    println("Numbers 1-10, skip evens, stop at 7:")
    for i in range(1, 11) {
        if i == 7 {
            break
        }
        if i % 2 == 0 {
            continue
        }
        println("  ${i}")
    }
}
